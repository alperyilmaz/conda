configfile: "config_trimmer.yaml"

rule all:
    input:
        expand('mapped/{sample}.sorted.bam.bai', sample=config["samples"])

rule trim_reads_pe:
    input:
        r1="data/pairedend/{sample}1.fastq",
        r2="data/pairedend/{sample}2.fastq"
    output:
        r1=temp("trimmed/{sample}1.fastq"),
        r2=temp("trimmed/{sample}2.fastq"),
        r1_unpaired= temp("trimmed/{sample}1.unpaired.fastq"),
        r2_unpaired= temp("trimmed/{sample}2.unpaired.fastq"),
        trimlog="trimmed/{sample}.trimlog.txt"   
    params:
        extra=lambda w, output: "-trimlog {}".format(output.trimlog),
        **config["params"]["trimmomatic"]["pe"]
        
    log:
       "logs/trimmomatic/{sample}.log"
    wrapper:
        "0.35.1/bio/trimmomatic/pe"


rule bwa_mem:
    input:
        reads=["trimmed/{sample}1.fastq", "trimmed/{sample}2.fastq"]
    output:
        "mapped/{sample}.sorted.bam"
    log:
        "logs/bwa_mem/{sample}.log"
    params:
        index="data/genome.fa",
        extra=r"-R '@RG\tID:{sample}\tSM:{sample}'",
        sort="samtools",             # Can be 'none', 'samtools' or 'picard'.
        sort_order="coordinate",     # Can be 'queryname' or 'coordinate'.
        sort_extra=""                # Extra args for samtools/picard.
    threads: 8
    wrapper:
        "0.35.1/bio/bwa/mem"
        
rule samtools_index:
    input:
        'mapped/{sample}.sorted.bam'
    output:
         'mapped/{sample}.sorted.bam.bai'
    shell:
         'samtools index {input}'

